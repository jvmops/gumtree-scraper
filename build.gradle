plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'jacoco'
	id "io.freefair.lombok" version "5.0.0-rc6"
	id 'org.barfuin.gradle.jacocolog' version '1.1.1'
}

group = 'com.jvmops'
version = '1.0.1'
sourceCompatibility = JavaVersion.VERSION_14
libsDirName = 'app'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

ext {
	jsoupVersion = '1.12.1'
	testcontainersVersion = '1.14.1'
	testcontainersEmbeddedMongoVersion = '1.48'
	springCloudVersion = 'Hoxton.SR4'
}

dependencies {
	implementation 'javax.validation:validation-api'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
	implementation "org.jsoup:jsoup:$jsoupVersion"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
	testImplementation "com.playtika.testcontainers:embedded-mongodb:$testcontainersEmbeddedMongoVersion"
	testImplementation "org.springframework.cloud:spring-cloud-starter"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
	jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
	jvmArgs += '--enable-preview'
}

bootJar {
	archiveFileName = "gumtree-scrapper.jar"
}

/**
 * Test configuration
 */

jacocoTestReport {
	reports {
		xml.destination file("${buildDir}/reports/jacoco/report.xml")
		csv.enabled false
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.6
			}
		}
	}
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
test {
	testLogging.showStandardStreams = false // extensive docker and spring logging

	afterSuite { desc, result ->
		if (!desc.parent)
			println("${result.resultType} " +
					"(${result.testCount} tests, " +
					"${result.successfulTestCount} successes, " +
					"${result.failedTestCount} failures, " +
					"${result.skippedTestCount} skipped)")
	}

	// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.logging.TestLoggingContainer.html
	testLogging {
		events "passed", "skipped", "failed"
	}

	useJUnitPlatform()
}
